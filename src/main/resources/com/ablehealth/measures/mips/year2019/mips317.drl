package com.ablehealth;

import com.ablehealth.model.*
import com.ablehealth.payloads.*;
import com.ablehealth.results.*;

import accumulate com.ablehealth.functions.LatestClinicalActivityWithLowestResultAccumulateFunction latestClinicalActivityWithLowestResult;

global PatientResults controlSet;

rule 'Year2019.Mips317.Denominator'
when
    $program: Program(rules contains 'measure.Mips.2019.Mips317')
    $encounter: ClinicalActivity(
        startsDuring($program.measurementPeriod),
        placeOfService != '02',
        codeModifiers not contains 'GQ' && not contains 'GT' && not contains '95',
        withValueSet("MipsYear2019", "MIPS_317_ENCOUNTER")
    )
    $patient: Patient(getAgeInYearsAt($program.measurementPeriod.start) >= 18)
then
    insert(new EncounterDenominator($program, $encounter, 'Year2019.Mips317'));
end

rule 'Year2019.Mips317.Exclusion.exclusion_1'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips317')
    $patient: Patient()
    (
        exists ClinicalActivity(
            startsDuring($program.measurementPeriod),
            withValueSet("MipsYear2019", "HYPERTENSION_EXCLUSION")
        )
        or exists Diagnosis(
            overlaps($encounter),
            withValueSet("VsacYear2019", "DIAGNOSIS_OF_HYPERTENSION")
        )
    )
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips317', MeasureStatusValue.EXCLUSION, "exclusion_1");
    controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips317', MeasureStatusValue.EXCLUSION, "exclusion_1");
end

rule 'Year2019.Mips317.Success.G8783'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips317')
    $patient: Patient()
    (
        exists ClinicalActivity(
            startsDuring($program.measurementPeriod),
            withValueSet("MipsYear2019", "PERFORMANCE_MET_G8783")
        )
        or (
            $normalDiastolics: ClinicalActivity(
                startsDuring($program.measurementPeriod),
                withValueSet("VsacYear2019", "DIASTOLIC_BLOOD_PRESSURE"),
                resultAsDecimal < 80
            )
            and exists ClinicalActivity(
                startsDuring($normalDiastolics),
                withValueSet("VsacYear2019", "SYSTOLIC_BLOOD_PRESSURE"),
                resultAsDecimal < 120
            )
        )
    )
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips317', MeasureStatusValue.SUCCESS, "G8783");
    controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips317', MeasureStatusValue.SUCCESS, "G8783");
end

rule 'Year2019.Mips317.Success.G8950'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips317')
    $patient: Patient()
    (
        exists ClinicalActivity(
            startsDuring($program.measurementPeriod),
            withValueSet("MipsYear2019", "PERFORMANCE_MET_G8950")
        )
        or (
            exists (
                ClinicalActivity(
                    startsDuring($program.measurementPeriod),
                    withValueSet("VsacYear2019", "LIFESTYLE_RECOMMENDATION") ||
                    withValueSet("VsacYear2019", "WEIGHT_REDUCTION_RECOMMENDED") ||
                    withValueSet("VsacYear2019", "DIETARY_RECOMMENDATIONS") ||
                    withValueSet("VsacYear2019", "PHYSICAL_ACTIVITY_RECOMMENDATION") ||
                    withValueSet("VsacYear2019", "MODERATION_OF_ETOH_CONSUMPTION_RECOMMENDATION") ||
                    withValueSet("VsacYear2019", "REFERRAL_TO_ALTERNATIVE_PROVIDER_PRIMARY_CARE_PROVIDER")
                )
            )
            and (
                accumulate(
                    ClinicalActivity(
                        startsDuring($program.measurementPeriod),
                        withValueSet("VsacYear2019", "SYSTOLIC_BLOOD_PRESSURE"),
                        resultAsDecimal >= 90
                    );
                    $preHypertensiveSystolicCount: count(1);
                    $preHypertensiveSystolicCount == 1
                )
                or (
                    accumulate(
                        ClinicalActivity(
                            startsDuring($program.measurementPeriod),
                            withValueSet("VsacYear2019", "DIASTOLIC_BLOOD_PRESSURE"),
                            resultAsDecimal >= 140
                        );
                        $preHypertensiveDiastolicCount: count(1);
                        $preHypertensiveDiastolicCount == 1
                    )
                )
            )
        )
        or (
            exists (
                ClinicalActivity(
                    startsDuring($program.measurementPeriod),
                    withValueSet("VsacYear2019", "LIFESTYLE_RECOMMENDATION") ||
                    withValueSet("VsacYear2019", "WEIGHT_REDUCTION_RECOMMENDED") ||
                    withValueSet("VsacYear2019", "DIETARY_RECOMMENDATIONS") ||
                    withValueSet("VsacYear2019", "PHYSICAL_ACTIVITY_RECOMMENDATION") ||
                    withValueSet("VsacYear2019", "MODERATION_OF_ETOH_CONSUMPTION_RECOMMENDATION") ||
                    withValueSet("VsacYear2019", "REFERRAL_TO_ALTERNATIVE_PROVIDER_PRIMARY_CARE_PROVIDER")
                )
            )
            and (
                accumulate(
                    ClinicalActivity(
                        startsDuring($program.measurementPeriod),
                        withValueSet("VsacYear2019", "SYSTOLIC_BLOOD_PRESSURE"),
                        resultAsDecimal >= 80
                    );
                    $hypertensiveSystolicCount: count(1);
                    $hypertensiveSystolicCount >= 2
                )
                or accumulate(
                    ClinicalActivity(
                        startsDuring($program.measurementPeriod),
                        withValueSet("VsacYear2019", "DIASTOLIC_BLOOD_PRESSURE"),
                        resultAsDecimal >= 120
                    );
                    $hypertensiveDiastolicCount: count(1);
                    $hypertensiveDiastolicCount >= 2
                )
            )
            and (
                exists LaboratoryTestPerformed(
                    startsDuring($program.measurementPeriod),
                    withValueSet("VsacYear2019", "LABORATORY_TESTS_FOR_HYPERTENSION")
                )
                or exists Medication(
                    overlaps($program.measurementPeriod),
                    withValueSet("VsacYear2019", "ANTI_HYPERTENSIVE_PHARMACOLOGIC_THERAPY")
                )
                or exists ClinicalActivity(
                    startsDuring($program.measurementPeriod),
                    withValueSet("VsacYear2019", "LABORATORY_TESTS_FOR_HYPERTENSION") ||
                    withValueSet("VsacYear2019", "ECG_12_LEAD_OR_STUDY_ORDER") ||
                    withValueSet("VsacYear2019", "REFERRAL_TO_ALTERNATIVE_PROVIDER_PRIMARY_CARE_PROVIDER")
                )
            )
        )
    )
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips317', MeasureStatusValue.SUCCESS, "G8950");
    controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips317', MeasureStatusValue.SUCCESS, 'G8950');
end

rule 'Year2019.Mips317.Exception.G9745'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips317')
    $patient: Patient()
    $bloodPressure: ClinicalActivity(
        startsDuring($program.measurementPeriod),
        withValueSet("VsacYear2019", "SYSTOLIC_BLOOD_PRESSURE")
    )
    $latestEncounterDuringBloodPressure: ClinicalActivity() from accumulate (
        $c: ClinicalActivity(
            startsDuring($bloodPressure),
            placeOfService != '02',
            codeModifiers not contains 'GQ' && not contains 'GT' && not contains '95',
            withValueSet("MipsYear2019", "MIPS_317_ENCOUNTER")
        ),
        latestClinicalActivityWithLowestResult($c)
    )
    and exists ClinicalActivity(
        startsDuring($latestEncounterDuringBloodPressure),
        withValueSet("MipsYear2019", "DENOMINATOR_EXCEPTION_G9745")
    )
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips317', MeasureStatusValue.EXCEPTION, "G9745");
    controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips317', MeasureStatusValue.EXCEPTION, 'G9745');
end

rule 'Year2019.Mips317.Gap.G8952'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips317')
    $patient: Patient()
    (
        exists ClinicalActivity(
            startsDuring($program.measurementPeriod),
            withValueSet("MipsYear2019", "PERFORMANCE_NOT_MET_G8952")
        )
        or (
            exists ClinicalActivity(
                startsDuring($program.measurementPeriod),
                withValueSet("VsacYear2019", "DIASTOLIC_BLOOD_PRESSURE"),
                resultAsDecimal >= 80
            )
            or exists ClinicalActivity(
                startsDuring($program.measurementPeriod),
                withValueSet("VsacYear2019", "SYSTOLIC_BLOOD_PRESSURE"),
                resultAsDecimal >= 120
            )
        )
    )
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips317', MeasureStatusValue.GAP, "G8952");
    controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips317', MeasureStatusValue.GAP, 'G8952');
end

rule 'Year2019.Mips317.Gap.G8785'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips317')
    $patient: Patient()
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips317', MeasureStatusValue.GAP, 'G8785');
    controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips317', MeasureStatusValue.GAP, 'G8785');
end
