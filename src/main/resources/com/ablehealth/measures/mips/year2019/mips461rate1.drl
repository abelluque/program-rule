package com.ablehealth;

import com.ablehealth.model.*
import com.ablehealth.payloads.*;
import com.ablehealth.results.*;

global PatientResults controlSet;

rule 'Year2019.Mips461Rate1.Denominator'
when
    $program: Program(rules contains 'measure.Mips.2019.Mips461Rate1')
    $encounter: ClinicalActivity(
        startsDuring($program.measurementPeriod.start.minusMonths(12),$program.measurementPeriod.end.minusMonths(12)),
        withValueSet("MipsYear2019", "MIPS_461_PROCEDURE")
    )
    exists Diagnosis(
        overlaps($encounter),
        withValueSet("MipsYear2019", "HERNIATED_DISC")
    )
    $patient: Patient(getAgeInYearsAt($program.measurementPeriod.start.minusMonths(12)) >= 18)
then
    insert(new EncounterDenominator($program, $encounter, 'Year2019.Mips461Rate1'));
end

rule 'Year2019.Mips461Rate1.Exclusion.exclusion_1'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips461Rate1')
    (
        exists ClinicalActivity(
            startsDuring($encounter),
            withValueSet("MipsYear2019", "OTHER_SPINE_PROCEDURE") || withValueSet("AbleYear2019", "OTHER_SPINE_PROCEDURE")
        )
    )
then
    controlSet.addEncounterMeasureStatus($program, $encounter, 'Year2019.Mips461Rate1', MeasureStatusValue.EXCLUSION, "exclusion_1");
end

rule 'Year2019.Mips461Rate1.Success.MipsG9947'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips461Rate1')
    (
        exists ClinicalActivity(
          startsDuring($encounter.startDate,$encounter.startDate.plusWeeks(20)),
          withValueSet("MipsYear2019", "PERFORMANCE_MET_G9947")
        )
        or (
          exists ClinicalActivity(
            startsDuring($encounter.startDate.minusMonths(3),$encounter.startDate),
            withValueSet("AbleYear2019", "LEG_VISUAL_ANALOG_SCALE")
          )
          and exists ClinicalActivity(
            startsDuring($encounter.startDate.plusWeeks(6),$encounter.startDate.plusWeeks(20)),
            withValueSet("AbleYear2019", "LEG_VISUAL_ANALOG_SCALE")
          )
        )
    )
then
    controlSet.addEncounterMeasureStatus($program, $encounter, 'Year2019.Mips461Rate1', MeasureStatusValue.SUCCESS, "G9947");
end

rule 'Year2019.Mips461Rate1.Gaps.MipsG9949'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips461Rate1')
then
    controlSet.addEncounterMeasureStatus($program, $encounter, 'Year2019.Mips461Rate1', MeasureStatusValue.GAP, "G9949");
end
