package com.ablehealth;

import com.ablehealth.model.*
import com.ablehealth.payloads.*;
import com.ablehealth.results.*;

global PatientResults controlSet;

rule 'Year2019.Mips012.Denominator'
when
    $program: Program(rules contains 'measure.Mips.2019.Mips012')
    $encounter: ClinicalActivity(
        startsDuring($program.measurementPeriod),
        placeOfService != '02',
        codeModifiers not contains 'GQ' && not contains 'GT' && not contains '95',
        withValueSet("MipsYear2019", "MIPS_012_ENCOUNTER")
    )
    and exists Diagnosis(
        overlaps($encounter),
        withValueSet("MipsYear2019", "PRIMARY_OPEN_ANGLE_GLAUCOMA_POAG")
    )
    $patient: Patient(
        getAgeInYearsAt($encounter.startDate) >= 18
    )
then
    insert(new EncounterDenominator($program, $encounter, 'Year2019.Mips012'));
end

rule 'Year2019.Mips012.Success.2027F'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips012')
    $patient: Patient()
    exists ClinicalActivity(
        startsDuring($program.measurementPeriod),
        withValueSet("AbleYear2019", "OPTIC_NERVE_HEAD_EVALUATION") ||
        (
            codeModifiers.isEmpty() &&
            withValueSet("MipsYear2019", "PERFORMANCE_MET_2027F")
        )
    )
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips012', MeasureStatusValue.SUCCESS, "2027F");
    controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips012', MeasureStatusValue.SUCCESS, "2027F");
end

rule 'Year2019.Mips012.Exception.2027F-1P'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips012')
    $patient: Patient()
    and exists ClinicalActivity(
        startsDuring($encounter),
        codeModifiers contains '1P',
        withValueSet("MipsYear2019", "DENOMINATOR_EXCEPTION_2027F_1P")
    )
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips012', MeasureStatusValue.EXCEPTION, "2027F-1P");
    controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips012', MeasureStatusValue.EXCEPTION, "2027F-1P");
end

rule 'Year2019.Mips012.Gap.2027F-8P'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips012')
    $patient: Patient()
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips012', MeasureStatusValue.GAP, "2027F-8P");
    controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips012', MeasureStatusValue.GAP, "2027F-8P");
end
