package com.ablehealth;

import com.ablehealth.model.*
import com.ablehealth.payloads.*;
import com.ablehealth.results.*;

global PatientResults controlSet;

rule 'Year2019.Mips047.Denominator'
when
	$program: Program(rules contains 'measure.Mips.2019.Mips047')
	$patient: Patient()
	$encounter: ClinicalActivity(
		startsDuring($program.measurementPeriod),
		placeOfService != '23',
		$patient.getAgeInYearsAt(startDate) >= 65,
		withValueSet("MipsYear2019", "MIPS_047_ENCOUNTER")
	)
then
	insert(new EncounterDenominator($program, $encounter, 'Year2019.Mips047'));
end

rule 'Year2019.Mips047.Exclusion.exclusion_1'
when
	EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips047')
	$patient: Patient()
	(
		exists ClinicalActivity(
			overlaps($program.measurementPeriod),
			withValueSet("VsacYear2019", "PALLIATIVE_CARE")
		)
		or
		exists ClinicalActivity(
			startsDuring($program.measurementPeriod),
			withValueSet("MipsYear2019", "MIPS_HOSPICE_SERVICE")
		)
	)
then
	controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips047', MeasureStatusValue.EXCLUSION, "exclusion_1");
	controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips047', MeasureStatusValue.EXCLUSION, "exclusion_1");
end

rule 'Year2019.Mips047.Success.1123F'
when
	EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips047')
	$patient: Patient()
	(
		exists ClinicalActivity(
			startsOnOrBefore($program.measurementPeriod.end),
			codeModifiers.isEmpty,
			withValueSet("MipsYear2019", "PERFORMANCE_MET_1123F")
		)
		or
		exists ClinicalActivity(
			startsOnOrBefore($program.measurementPeriod.end),
			withValueSet("AbleYear2019", "ADVANCE_DIRECTIVE")
		)
	)
then
	controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips047', MeasureStatusValue.SUCCESS, "1123F");
	controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips047', MeasureStatusValue.SUCCESS, "1123F");
end

rule 'Year2019.Mips047.Success.1124F'
when
	EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips047')
	$patient: Patient()
	(
		exists ClinicalActivity(
			startsOnOrBefore($program.measurementPeriod.end),
			withValueSet("MipsYear2019", "PERFORMANCE_MET_1124F")
		)
		or
		exists ClinicalActivity(
			startsOnOrBefore($program.measurementPeriod.end),
			withValueSet("AbleYear2019", "ADVANCE_DIRECTIVE_DISCUSSED")
		)
	)
then
	controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips047', MeasureStatusValue.SUCCESS, "1124F");
	controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips047', MeasureStatusValue.SUCCESS, "1124F");
end

rule 'Year2019.Mips047.Gap.1123F-8P'
when
	EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips047')
	$patient: Patient()
then
	controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips047', MeasureStatusValue.GAP, "1123F-8P");
	controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips047', MeasureStatusValue.GAP, "1123F-8P");
end