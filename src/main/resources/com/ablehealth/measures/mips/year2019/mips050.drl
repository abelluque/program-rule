package com.ablehealth;

import com.ablehealth.model.*
import com.ablehealth.payloads.*;
import com.ablehealth.results.*;

global PatientResults controlSet;

rule 'Year2019.Mips050.Denominator'
when
	$program: Program(rules contains 'measure.Mips.2019.Mips050')
	$patient: Patient(
		isFemale()
	)
	$encounter: ClinicalActivity(
		startsDuring($program.measurementPeriod),
		$patient.getAgeInYearsAt(startDate) >= 65,
		withValueSet("MipsYear2019", "MIPS_050_ENCOUNTER")
	)
	and
	exists Diagnosis(
		overlaps($program.measurementPeriod),
		withValueSet("MipsYear2019", "URINARY_INCONTINENCE")
	)
then
	insert(new EncounterDenominator($program, $encounter, 'Year2019.Mips050'));
end

rule 'Year2019.Mips050.Exclusion.denominator_exclusion'
when
	EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips050')
	$patient: Patient()
	(
		exists ClinicalActivity(
			overlaps($program.measurementPeriod),
			withValueSet("VsacYear2019", "PALLIATIVE_CARE")
		)
		or
		exists ClinicalActivity(
			startsDuring($program.measurementPeriod),
			withValueSet("MipsYear2019", "MIPS_HOSPICE_SERVICE")
		)
	)
then
	controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips050', MeasureStatusValue.EXCLUSION, "denominator_exclusion");
	controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips050', MeasureStatusValue.EXCLUSION, "denominator_exclusion");
end

rule 'Year2019.Mips050.Success.0509F'
when
	EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips050')
	$patient: Patient()
	(
		exists ClinicalActivity(
			startsDuring($program.measurementPeriod),
			codeModifiers.isEmpty,
			withValueSet("MipsYear2019", "PERFORMANCE_MET_0509F")
		)
		or
		exists ClinicalActivity(
			startsDuring($program.measurementPeriod),
			withValueSet("AbleYear2019", "URINARY_INCONTINENCE_INTERVENTION") ||
			withValueSet("VsacYear2019", "UROLOGICAL_SURGERY")
		)
		or
		exists Medication(
			overlaps($program.measurementPeriod),
			withValueSet("AbleYear2019", "URINARY_INCONTINENCE_MEDICATION")
		)
		or
		exists LaboratoryTestPerformed(
			startsDuring($program.measurementPeriod),
			withValueSet("AbleYear2019", "URINALYSIS")
		)
	)
then
	controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips050', MeasureStatusValue.SUCCESS, "0509F");
	controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips050', MeasureStatusValue.SUCCESS, "0509F");
end

rule 'Year2019.Mips050.Gap.0509F-8P'
when
	EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips050')
	$patient: Patient()
then
	controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips050', MeasureStatusValue.GAP, "0509F-8P");
	controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips050', MeasureStatusValue.GAP, "0509F-8P");
end

