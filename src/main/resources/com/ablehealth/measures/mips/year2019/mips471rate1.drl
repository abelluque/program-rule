package com.ablehealth;

import com.ablehealth.model.*
import com.ablehealth.payloads.*;
import com.ablehealth.results.*;

global PatientResults controlSet;

rule 'Year2019.Mips471Rate1.Denominator'
when
    $program: Program(rules contains 'measure.Mips.2019.Mips471Rate1')
    $encounter: ClinicalActivity(
        startsDuring($program.measurementPeriod.start.minusMonths(12),$program.measurementPeriod.end.minusMonths(12)),
        withValueSet("MipsYear2019", "MIPS_471_PROCEDURE")
    )
    exists Diagnosis(
        overlaps($encounter),
        withValueSet("MipsYear2019", "HERNIATED_DISC")
    )
    $patient: Patient(getAgeInYearsAt($program.measurementPeriod.start.minusMonths(12)) >= 18)
then
    insert(new EncounterDenominator($program, $encounter, 'Year2019.Mips471Rate1'));
end

rule 'Year2019.Mips471Rate1.Exclusion.exclusion_1'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips471Rate1')
    (
        exists ClinicalActivity(
            startsDuring($encounter),
            withValueSet("MipsYear2019", "OTHER_SPINE_PROCEDURE") || withValueSet("AbleYear2019", "OTHER_SPINE_PROCEDURE")
        )
    )
then
    controlSet.addEncounterMeasureStatus($program, $encounter, 'Year2019.Mips471Rate1', MeasureStatusValue.EXCLUSION, "exclusion_1");
end

rule 'Year2019.Mips471Rate1.Success.MipsM1048'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips471Rate1')
    (
        exists ClinicalActivity(
          startsDuring($encounter.startDate,$encounter.startDate.plusWeeks(20)),
          withValueSet("MipsYear2019", "PERFORMANCE_MET_M1048")
        )
        or (
          exists ClinicalActivity(
            startsDuring($encounter.startDate.minusMonths(3),$encounter.startDate),
            withValueSet("AbleYear2019", "OSWESTRY_DISABILITY_INDEX")
          )
          and exists ClinicalActivity(
            startsDuring($encounter.startDate.plusWeeks(6),$encounter.startDate.plusWeeks(20)),
            withValueSet("AbleYear2019", "OSWESTRY_DISABILITY_INDEX")
          )
        )
    )
then
    controlSet.addEncounterMeasureStatus($program, $encounter, 'Year2019.Mips471Rate1', MeasureStatusValue.SUCCESS, "M1048");
end

rule 'Year2019.Mips471Rate1.Gaps.MipsM1049'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips471Rate1')
then
    controlSet.addEncounterMeasureStatus($program, $encounter, 'Year2019.Mips471Rate1', MeasureStatusValue.GAP, "M1049");
end
