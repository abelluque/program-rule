package com.ablehealth;

import com.ablehealth.model.*
import com.ablehealth.payloads.*;
import com.ablehealth.results.*;

global PatientResults controlSet;

rule 'Year2019.Mips048.Denominator'
when
	$program: Program(rules contains 'measure.Mips.2019.Mips048')
	$patient: Patient(
		isFemale()
	)
	$encounter: ClinicalActivity(
		startsDuring($program.measurementPeriod),
		$patient.getAgeInYearsAt(startDate) >= 65,
		withValueSet("MipsYear2019", "MIPS_048_ENCOUNTER")
	)
then
	insert(new EncounterDenominator($program, $encounter, 'Year2019.Mips048'));
end

rule 'Year2019.Mips048.Exclusion.denominator_exclusion'
when
	EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips048')
	$patient: Patient()
	(
		exists ClinicalActivity(
			overlaps($program.measurementPeriod),
			withValueSet("VsacYear2019", "PALLIATIVE_CARE")
		)
		or
		exists ClinicalActivity(
			startsDuring($program.measurementPeriod),
			withValueSet("MipsYear2019", "MIPS_HOSPICE_SERVICE")
		)
	)
then
	controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips048', MeasureStatusValue.EXCLUSION, "denominator_exclusion");
	controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips048', MeasureStatusValue.EXCLUSION, "denominator_exclusion");
end

rule 'Year2019.Mips048.Success.1090F'
when
	EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips048')
	$patient: Patient()
	(
		exists ClinicalActivity(
			startsDuring($program.measurementPeriod),
			codeModifiers == null || codeModifiers.isEmpty,
			withValueSet("MipsYear2019", "PERFORMANCE_MET_1090F")
		)
		or
		exists ClinicalActivity(
			startsDuring($program.measurementPeriod),
			withValueSet("AbleYear2019", "URINARY_INCONTINENCE_ASSESSMENT")
		)
	)
then
	controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips048', MeasureStatusValue.SUCCESS, "1090F");
	controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips048', MeasureStatusValue.SUCCESS, "1090F");
end

rule 'Year2019.Mips048.Gap.1090F-8P'
when
	EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips048')
	$patient: Patient()
then
	controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips048', MeasureStatusValue.GAP, "1090F-8P");
	controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips048', MeasureStatusValue.GAP, "1090F-8P");
end

