package com.ablehealth;

import com.ablehealth.model.*
import com.ablehealth.payloads.*;
import com.ablehealth.results.*;

global PatientResults controlSet;

rule 'Year2019.Mips113.Denominator'
when
    $program: Program(rules contains 'measure.Mips.2019.Mips113')
    $mips113encounter: ClinicalActivity(
        startsDuring($program.measurementPeriod),
        placeOfService not in ('32','33','34','54','56'),
        withValueSet("MipsYear2019", "MIPS_113_ENCOUNTER")
    )
    exists Patient(
        getAgeInYearsAt($mips113encounter.startDate) >= 50,
        getAgeInYearsAt($mips113encounter.startDate) < 75
    )
then
    insert(new EncounterDenominator($program, $mips113encounter, 'Year2019.Mips113'));
end

rule 'Year2019.Mips113.Exclusion.exclusion_1'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips113')
    $patient: Patient()
    (
        exists Diagnosis(
            startsBefore($program.measurementPeriod.end),
            withValueSet("VsacYear2019", "MALIGNANT_NEOPLASM_OF_COLON") || withValueSet("HedisYear2019", "COLORECTAL_CANCER")
        )
        or exists ClinicalActivity(
            startsBefore($program.measurementPeriod.end),
            withValueSet("VsacYear2019", "TOTAL_COLECTOMY") || withValueSet("HedisYear2019", "TOTAL_COLECTOMY") || withValueSet("MipsYear2019", "HISTORY_OF_TOTAL_COLECTOMY_OR_COLORECTAL_CANCER")
        )
    )
then
  controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips113', MeasureStatusValue.EXCLUSION, "exclusion_1");
  controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips113', MeasureStatusValue.EXCLUSION, "exclusion_1");
end

rule 'Year2019.Mips113.Exclusion.exclusion_2'
when
  EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips113')
  $patient: Patient()
  exists ClinicalActivity(
    startsDuring($program.measurementPeriod),
    withValueSet("MipsYear2019", "MIPS_HOSPICE_SERVICE") || withValueSet("VsacYear2019", "PALLIATIVE_CARE")
  )
then
  controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips113', MeasureStatusValue.EXCLUSION, "exclusion_2");
  controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips113', MeasureStatusValue.EXCLUSION, "exclusion_2");
end

rule 'Year2019.Mips113.Exclusion.exclusion_3'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips113')
    $patient: Patient()
    (
        (
            $institutionalProcedure: ClinicalActivity(
                startsDuring($program.measurementPeriod),
                withValueSet("MipsYear2019", "INSTITUTIONAL_EXCLUSION")
            )
            and exists Patient(
                getAgeInYearsAt($institutionalProcedure.startDate) >= 65
            )
        )
        or (
            $careServicesProcedure: ClinicalActivity(
                startsDuring($program.measurementPeriod),
                placeOfService in ('32','33','34','54','56'),
                withValueSet("VsacYear2019", "CARE_SERVICES_IN_LONG_TERM_RESIDENTIAL_FACILITY")
            )
            and exists Patient(
                getAgeInYearsAt($careServicesProcedure.startDate) >= 65
            )
        )
    )
then
  controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips113', MeasureStatusValue.EXCLUSION, "exclusion_3");
  controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips113', MeasureStatusValue.EXCLUSION, "exclusion_3");
end

rule 'Year2019.Mips113.Success.3017F'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips113')
    $patient: Patient()
    (
        exists ClinicalActivity(
            startsDuring($program.measurementPeriod),
            withValueSet("MipsYear2019", "PERFORMANCE_MET_3017F"),
            codeModifiers not contains 'GQ' && not contains 'GT' && not contains '95'
        )
        or exists ClinicalActivity(
            startsDuring($program.measurementPeriod.start.minusYears(9), $program.measurementPeriod.end),
            withValueSet("VsacYear2019", "COLONOSCOPY")
        )
        or exists ClinicalActivity(
            startsDuring($program.measurementPeriod.start.minusYears(4), $program.measurementPeriod.end),
            withValueSet("VsacYear2019", "FLEXIBLE_SIGMOIDOSCOPY")
        )
        or exists LaboratoryTestPerformed(
            startsDuring($program.measurementPeriod),
            withValueSet("VsacYear2019", "FECAL_OCCULT_BLOOD_TEST_FOBT")
        )
        or exists ClinicalActivity(
            startsDuring($program.measurementPeriod),
            withValueSet("VsacYear2019", "FECAL_OCCULT_BLOOD_TEST_FOBT")
        )
        or exists LaboratoryTestPerformed(
            startsDuring($program.measurementPeriod.start.minusYears(2), $program.measurementPeriod.end),
            withValueSet("VsacYear2019", "FIT_DNA")
        )
        or exists ClinicalActivity(
            startsDuring($program.measurementPeriod.start.minusYears(2), $program.measurementPeriod.end),
            withValueSet("HedisYear2019", "FIT_DNA")
        )
        or exists ClinicalActivity(
            startsDuring($program.measurementPeriod.start.minusYears(4), $program.measurementPeriod.end),
            withValueSet("VsacYear2019", "CT_COLONOGRAPHY")
        )
    )
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips113', MeasureStatusValue.SUCCESS, "3017F");
    controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips113', MeasureStatusValue.SUCCESS, "3017F");
end

rule 'Year2019.Mips113.Gap.3017F8P'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips113')
    $patient: Patient()
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips113', MeasureStatusValue.GAP, "3017F-8P");
    controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips113', MeasureStatusValue.GAP, "3017F-8P");
end
