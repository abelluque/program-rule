package com.ablehealth;

import com.ablehealth.model.*
import com.ablehealth.payloads.*;
import com.ablehealth.results.*;

global PatientResults controlSet;

rule 'Year2019.Mips137.Denominator'
when
	$program: Program(rules contains 'measure.Mips.2019.Mips137')
	$patient: Patient()
	$encounter: ClinicalActivity(
		startsDuring($program.measurementPeriod),
		!hasModifier("GQ"),
		!hasModifier("GT"),
		!hasModifier("95"),
		withValueSet("MipsYear2019", "MIPS_137_ENCOUNTER"),
		placeOfService == null || placeOfService != '02'
	)
	and Diagnosis(
		overlaps($encounter),
		withValueSet("MipsYear2019", "MELANOMA")
	)
	
then
	insert(new EncounterDenominator($program, $encounter, 'Year2019.Mips137'));
end

rule 'Year2019.Mips137.Success.7010F'
when
	EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips137')
	$patient: Patient()
	ClinicalActivity(
		startsDuring($program.measurementPeriod),
		codeModifiers.isEmpty(),
		withValueSet("MipsYear2019", "PERFORMANCE_MET_7010F")
	)
then
	controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips137', MeasureStatusValue.SUCCESS, "7010F");
	controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips137', MeasureStatusValue.SUCCESS, "7010F");
end

rule 'Year2019.Mips137.Exception.7010F-3P'
when
	EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips137')
	$patient: Patient()
	ClinicalActivity(
		startsDuring($program.measurementPeriod),
		hasModifier("3P"),
		withValueSet("MipsYear2019", "DENOMINATOR_EXCEPTION_7010F_3P")
	)
then
	controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips137', MeasureStatusValue.EXCEPTION, "7010F-3P");
	controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips137', MeasureStatusValue.EXCEPTION, "7010F-3P");
end

rule 'Year2019.Mips137.Gap.7010F-8P'
when
	EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips137')
	$patient: Patient()
then
	controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips137', MeasureStatusValue.GAP, "7010F-8P");
	controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips137', MeasureStatusValue.GAP, "7010F-8P");
end

