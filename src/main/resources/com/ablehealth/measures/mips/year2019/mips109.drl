package com.ablehealth;

import com.ablehealth.model.*
import com.ablehealth.payloads.*;
import com.ablehealth.results.*;

global PatientResults controlSet;

rule 'Year2019.Mips109.Denominator'
when
    $program: Program(rules contains 'measure.Mips.2019.Mips109')
    $encounter: ClinicalActivity(
        startsDuring($program.measurementPeriod),
        withValueSet("MipsYear2019", "MIPS_109_ENCOUNTER")
    )
    $patient: Patient(
        getAgeInYearsAt($encounter.startDate) >= 21
    )
    and exists Diagnosis(
        overlaps($encounter),
        withValueSet("MipsYear2019", "OSTEOARTHRITIS")
    )
then
    insert(new EncounterDenominator($program, $encounter, 'Year2019.Mips109'));
end

rule 'Year2019.Mips109.Success.1006F'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips109')
    $patient: Patient()
    and (
        exists ClinicalActivity(
            startsDuring($program.measurementPeriod),
            (
                withValueSet("MipsYear2019", "PERFORMANCE_MET_1006F") &&
                codeModifiers.isEmpty
            ) ||
            withValueSet("VsacYear2019", "FUNCTIONAL_STATUS_ASSESSMENT_FOR_HIP_REPLACEMENT") ||
            withValueSet("VsacYear2019", "FUNCTIONAL_STATUS_ASSESSMENT_FOR_KNEE_REPLACEMENT")
        )
        or (
            exists ClinicalActivity(
                startsDuring($program.measurementPeriod),
                withValueSet("AbleYear2019", "FUNCTIONAL_STATUS_ASSESSMENT")
            )
            and exists ClinicalActivity(
                startsDuring($program.measurementPeriod),
                withValueSet("AbleYear2019", "STANDARDIZED_PAIN_ASSESSMENT_TOOL") ||
                withValueSet("AbleYear2019", "PAIN_ASSESSMENTS")
            )
        )
    )
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips109', MeasureStatusValue.SUCCESS, "1006F");
    controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips109', MeasureStatusValue.SUCCESS, "1006F");
end

rule 'Year2019.Mips109.Gap.1006F-8P'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips109')
    $patient: Patient()
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips109', MeasureStatusValue.GAP, "1006F-8P");
    controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips109', MeasureStatusValue.GAP, "1006F-8P");
end
