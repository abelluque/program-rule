package com.ablehealth;

import com.ablehealth.model.*
import com.ablehealth.payloads.*;
import com.ablehealth.results.*;

global PatientResults controlSet;

rule 'Year2019.Mips176.Denominator'
when
	$program: Program(rules contains 'measure.Mips.2019.Mips176')
	$mips176encounter: ClinicalActivity(
		startsDuring($program.measurementPeriod),
		placeOfService != '02',
		codeModifiers not contains 'GQ' && not contains 'GT' && not contains '95',
		withValueSet("MipsYear2019", "MIPS_176_ENCOUNTER")
	)
	$patient: Patient(
		getAgeInYearsAt($mips176encounter.startDate) >= 18
	)
	and exists (
		Diagnosis(
			overlaps($mips176encounter),
			withValueSet("MipsYear2019", "RHEUMATOID_ARTHRITIS")
		)
	)
	and (
		exists (
			ClinicalActivity(
				startsDuring($program.measurementPeriod),
				withValueSet("MipsYear2019", "FIRST_TIME_ANTI_RHEUMATIC_DRUG_THERAPY")
			)
		)
		or (
			exists (
				Medication(
					startsDuring($program.measurementPeriod),
					withValueSet("AbleYear2019", "DMARD_MEDICATION") ||
					withValueSet("HedisYear2019", "DMARD")
				)
			)
			and not (
				exists (
					Medication(
						startsBefore($program.measurementPeriod.start),
						withValueSet("AbleYear2019", "DMARD_MEDICATION") ||
						withValueSet("HedisYear2019", "DMARD")
					)
				)
			)
		)
	)
then
	insert(new EncounterDenominator($program, $mips176encounter, 'Year2019.Mips176'));
end

rule 'Year2019.Mips176.Success.M1003'
when
	EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips176')
	$patient: Patient()
	(
		exists ClinicalActivity(
			startsDuring($program.measurementPeriod),
			withValueSet("MipsYear2019", "PERFORMANCE_MET_M1003")
		)
	)
then
	controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips176', MeasureStatusValue.SUCCESS, "M1003");
	controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips176', MeasureStatusValue.SUCCESS, "M1003");
end

rule 'Year2019.Mips176.Exception.M1004'
when
	EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips176')
	$patient: Patient()
	(
		exists ClinicalActivity(
			startsDuring($encounter),
			withValueSet("MipsYear2019", "DENOMINATOR_EXCEPTION_M1004")
		)
	)
then
	controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips176', MeasureStatusValue.EXCEPTION, "M1004");
	controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips176', MeasureStatusValue.EXCEPTION, "M1004");
end

rule 'Year2019.Mips176.Gap.M1005'
when
	EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips176')
	$patient: Patient()
then
	controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips176', MeasureStatusValue.GAP, "M1005");
	controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips176', MeasureStatusValue.GAP, "M1005");
end
