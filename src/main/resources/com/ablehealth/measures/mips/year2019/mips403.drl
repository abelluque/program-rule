package com.ablehealth;

import com.ablehealth.model.*
import com.ablehealth.payloads.*;
import com.ablehealth.results.*;

global PatientResults controlSet;

rule 'Year2019.Mips403.Denominator'
when
  $program: Program(rules contains 'measure.Mips.2019.Mips403')
  $encounter: ClinicalActivity(
    startsDuring($program.measurementPeriod),
    withValueSet("MipsYear2019", "MIPS_403_ENCOUNTER")
  )
  exists Diagnosis(
    overlaps($encounter),
    withValueSet("MipsYear2019", "ESRD")
  )
  exists ClinicalActivity(
    startsDuring($program.measurementPeriod),
    withValueSet("MipsYear2019", "PATIENT_DISCONTINUED_DIALYSIS")
  )
  $patient: Patient(getAgeInYearsAt($encounter.startDate) >= 18)
then
  insert(new EncounterDenominator($program, $encounter, 'Year2019.Mips403'));
end

rule 'Year2019.Mips403.Success.G9524'
when
  EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips403')
  $patient: Patient()
  exists ClinicalActivity(
    startsDuring($program.measurementPeriod),
    withValueSet("MipsYear2019", "PERFORMANCE_MET_G9524") || withValueSet("MipsYear2019", "MIPS_HOSPICE_SERVICE") || withValueSet("VsacYear2019", "PALLIATIVE_CARE")
  )
then
  controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips403', MeasureStatusValue.SUCCESS, "G9524");
  controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips403', MeasureStatusValue.SUCCESS, 'G9524');
end

rule 'Year2019.Mips403.Exception.G9525'
when
  EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips403')
  $patient: Patient()
  exists ClinicalActivity(
    startsDuring($program.measurementPeriod),
    withValueSet("MipsYear2019", "DENOMINATOR_EXCEPTION_G9525")
  )
then
  controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips403', MeasureStatusValue.EXCEPTION, "G9525");
  controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips403', MeasureStatusValue.EXCEPTION, 'G9525');
end

rule 'Year2019.Mips403.Gap.G9526'
when
  EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips403')
  $patient: Patient()
then
  controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips403', MeasureStatusValue.GAP, "G9526");
  controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips403', MeasureStatusValue.GAP, 'G9526');
end
