package com.ablehealth;

import com.ablehealth.model.*
import com.ablehealth.payloads.*;
import com.ablehealth.results.*;
import java.time.temporal.ChronoUnit;

global PatientResults controlSet;

rule 'Year2019.Mips374.Denominator'
when
    $program: Program(rules contains 'measure.Mips.2019.Mips374')
    $mips374encounter: ClinicalActivity(
        startsDuring($program.measurementPeriod),
        placeOfService != '02',
        codeModifiers not contains 'GQ' && not contains 'GT' && not contains '95',
        withValueSet("MipsYear2019", "MIPS_374_ENCOUNTER")
    )
    exists ClinicalActivity(
        startsDuring($program.measurementPeriod),
        withValueSet("MipsYear2019", "REFERRAL") || withValueSet("VsacYear2019", "REFERRAL")
    )
then
    insert(new EncounterDenominator($program, $mips374encounter, 'Year2019.Mips374'));
end

rule 'Year2019.Mips235.Success.G9969'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips374')
    $patient: Patient()
    (
        exists ClinicalActivity(
            startsDuring($program.measurementPeriod),
            withValueSet("MipsYear2019", "PERFORMANCE_MET_G9969")
        )
    )
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips374', MeasureStatusValue.SUCCESS, 'G9969');
    controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips374', MeasureStatusValue.SUCCESS, 'G9969');
end

rule 'Year2019.Mips235.Gaps.G9970'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips374')
    $patient: Patient()
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips374', MeasureStatusValue.GAP, 'G9970');
    controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips374', MeasureStatusValue.GAP, 'G9970');
end
