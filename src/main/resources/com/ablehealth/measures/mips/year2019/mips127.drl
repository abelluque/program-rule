package com.ablehealth;

import com.ablehealth.model.*
import com.ablehealth.payloads.*;
import com.ablehealth.results.*;

global PatientResults controlSet;

rule 'Year2019.Mips127.Denominator'
when
    $program: Program(rules contains 'measure.Mips.2019.Mips127')
    $encounter: ClinicalActivity(
        startsDuring($program.measurementPeriod),
        placeOfService != '02',
        codeModifiers not contains 'GQ' && not contains 'GT' && not contains '95',
        withValueSet("MipsYear2019", "MIPS_127_ENCOUNTER")
    )
    $patient: Patient(
        getAgeInYearsAt($encounter.startDate) >= 18
    )
    and exists Diagnosis(
        overlaps($encounter),
        withValueSet("MipsYear2019", "MIPS_127_DIABETES")
    )
then
    insert(new EncounterDenominator($program, $encounter, 'Year2019.Mips127'));
end

rule 'Year2019.Mips127.Success.G8410'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips127')
    $patient: Patient()
    and exists ClinicalActivity(
        startsDuring($encounter.startDate.minusMonths(12), $encounter.startDate),
        withValueSet("MipsYear2019", "PERFORMANCE_MET_G8410") ||
        withValueSet("AbleYear2019", "FOOTWEAR_EVALUATION")
    )
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips127', MeasureStatusValue.SUCCESS, "G8410");
    controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips127', MeasureStatusValue.SUCCESS, "G8410");
end

rule 'Year2019.Mips127.Exception.G8416'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips127')
    $patient: Patient()
    and exists ClinicalActivity(
        startsDuring($encounter),
        withValueSet("MipsYear2019", "DENOMINATOR_EXCEPTION_G8416")
    )
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips127', MeasureStatusValue.EXCEPTION, "G8416");
    controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips127', MeasureStatusValue.EXCEPTION, "G8416");
end

rule 'Year2019.Mips127.Gap.G8415'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips127')
    $patient: Patient()
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips127', MeasureStatusValue.GAP, "G8415");
    controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId,'Year2019.Mips127', MeasureStatusValue.GAP, "G8415");
end

