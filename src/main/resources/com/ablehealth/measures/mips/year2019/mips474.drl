package com.ablehealth;

import com.ablehealth.model.*
import com.ablehealth.payloads.*;
import com.ablehealth.results.*;

global PatientResults controlSet;

rule 'Year2019.Mips474.Denominator'
when
  $program: Program(rules contains 'measure.Mips.2019.Mips474')
  $encounter: ClinicalActivity(
    startsDuring($program.measurementPeriod),
    withValueSet("MipsYear2019", "MIPS_474_ENCOUNTER"),
    placeOfService != '02',
    codeModifiers not contains 'GQ' && not contains 'GT' && not contains '95'
  )
  $patient: Patient(getAgeInYearsAt($encounter.startDate) >= 50)
then
  insert(new EncounterDenominator($program, $encounter, 'Year2019.Mips474'));
end

rule 'Year2019.Mips474.Exclusion.M1061'
when
  EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips474')
  $patient: Patient()
  (
    exists ClinicalActivity(
      startsDuring($program.measurementPeriod),
      withValueSet("MipsYear2019", "DENOMINATOR_EXCLUSION_M1061")
    )
    or exists Diagnosis(
      overlaps($program.measurementPeriod),
      withValueSet("VsacYear2019", "PREGNANCY_DX")
    )
  )
then
  controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips474', MeasureStatusValue.EXCLUSION, "M1061");
  controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips474', MeasureStatusValue.EXCLUSION, 'M1061');
end

rule 'Year2019.Mips474.Exclusion.M1062'
when
  EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips474')
  $patient: Patient()
  (
    exists ClinicalActivity(
      startsDuring($program.measurementPeriod),
      withValueSet("MipsYear2019", "DENOMINATOR_EXCLUSION_M1062")
    )
    or exists Diagnosis(
      overlaps($program.measurementPeriod),
      withValueSet("AbleYear2019", "IMMUNODEFICIENT_CONDITIONS")
    )
  )
then
  controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips474', MeasureStatusValue.EXCLUSION, "M1062");
  controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips474', MeasureStatusValue.EXCLUSION, 'M1062');
end

rule 'Year2019.Mips474.Exclusion.M1063'
when
  EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips474')
  $patient: Patient()
  exists ClinicalActivity(
    startsDuring($program.measurementPeriod),
    withValueSet("MipsYear2019", "DENOMINATOR_EXCLUSION_M1063")
  )
then
  controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips474', MeasureStatusValue.EXCLUSION, "M1063");
  controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips474', MeasureStatusValue.EXCLUSION, 'M1063');
end

rule 'Year2019.Mips474.Success.M1064'
when
  EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips474')
  $patient: Patient()
  (
    exists ClinicalActivity(
      startsOnOrBefore($program.measurementPeriod.end),
      withValueSet("MipsYear2019", "PERFORMANCE_MET_M1064")
    )
    or (
      $vaccine1: Medication(
        startsOnOrBefore($program.measurementPeriod.end),
        withValueSet("AbleYear2019", "SHINGRIX_VACCINE")
      )
      and exists Medication(
        startsDuring($vaccine1.startDate.plusMonths(2),$vaccine1.startDate.plusMonths(6)),
        withValueSet("AbleYear2019", "SHINGRIX_VACCINE")
      )
    )
  )
then
  controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips474', MeasureStatusValue.SUCCESS, "M1064");
  controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips474', MeasureStatusValue.SUCCESS, 'M1064');
end

rule 'Year2019.Mips474.Exception.M1065'
when
  EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips474')
  $patient: Patient()
  (
    exists ClinicalActivity(
      startsOnOrBefore($program.measurementPeriod.end),
      withValueSet("MipsYear2019", "DENOMINATOR_EXCEPTION_M1065")
    )
    or exists Allergy(
      overlaps($program.measurementPeriod),
      withValueSet("AbleYear2019", "SHINGRIX_VACCINE")
    )
    or exists Medication(
      startsDuring($program.measurementPeriod.end.minusMonths(3).plusDays(1),$program.measurementPeriod.end),
      withValueSet("AbleYear2019", "SHINGRIX_VACCINE")
    )
  )
then
  controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips474', MeasureStatusValue.EXCEPTION, "M1065");
  controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips474', MeasureStatusValue.EXCEPTION, 'M1065');
end

rule 'Year2019.Mips474.Gap.M1066'
when
  EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips474')
  $patient: Patient()
then
  controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips474', MeasureStatusValue.GAP, 'M1066');
  controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips474', MeasureStatusValue.GAP, 'M1066');
end
