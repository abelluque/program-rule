package com.ablehealth;

import com.ablehealth.model.*
import com.ablehealth.payloads.*;
import com.ablehealth.results.*;

global PatientResults controlSet;

rule 'Year2019.Mips401.Denominator'
when
  $program: Program(rules contains 'measure.Mips.2019.Mips401')
  $encounter: ClinicalActivity(
    startsDuring($program.measurementPeriod),
    withValueSet("MipsYear2019", "MIPS_401_ENCOUNTER")
  )
  exists Diagnosis(
    overlaps($encounter),
    withValueSet("MipsYear2019", "MIPS_401_CHRONIC_HEPATITIS_C")
  )
  exists Diagnosis(
    overlaps($encounter),
    withValueSet("MipsYear2019", "CIRRHOSIS")
  )
then
  insert(new EncounterDenominator($program, $encounter, 'Year2019.Mips401'));
end

rule 'Year2019.Mips401.Success.G9455'
when
  EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips401')
  $patient: Patient()
  exists ClinicalActivity(
    startsDuring($program.measurementPeriod),
    withValueSet("MipsYear2019", "PERFORMANCE_MET_G9455") || withValueSet("AbleYear2019", "IMAGING_FOR_HEPATOCELLULAR_CARCINOMA")
  )
then
  controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips401', MeasureStatusValue.SUCCESS, "G9455");
  controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips401', MeasureStatusValue.SUCCESS, 'G9455');
end

rule 'Year2019.Mips401.Exception.G9456'
when
  EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips401')
  $patient: Patient()
  exists ClinicalActivity(
    startsDuring($program.measurementPeriod),
    withValueSet("MipsYear2019", "DENOMINATOR_EXCEPTION_G9456")
  )
then
  controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips401', MeasureStatusValue.EXCEPTION, "G9456");
  controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips401', MeasureStatusValue.EXCEPTION, 'G9456');
end

rule 'Year2019.Mips401.Gap.G9457'
when
  EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2019.Mips401')
  $patient: Patient()
then
  controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Mips401', MeasureStatusValue.GAP, 'G9457');
  controlSet.addProviderPatientMeasureStatus($program, $patient, $encounter.providerExternalId, 'Year2019.Mips401', MeasureStatusValue.GAP, 'G9457');
end
