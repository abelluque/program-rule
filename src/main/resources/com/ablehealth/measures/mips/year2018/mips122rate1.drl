package com.ablehealth;

import com.ablehealth.model.*
import com.ablehealth.payloads.*;
import com.ablehealth.results.*;

global PatientResults controlSet;

rule 'Year2018.Mips122Rate1.Denominator'
when
    $program: Program(rules contains 'measure.Mips.2018.Mips122Rate1')
    $encounter: ClinicalActivity(
        startsDuring($program.measurementPeriod),
        placeOfService != '02',
        codeModifiers not contains 'GQ' && not contains 'GT' && not contains '95',
        withValueSet("MipsYear2018", "MIPS_122_ENCOUNTER")
    )
    exists Diagnosis(
        overlaps($encounter),
        withValueSet("MipsYear2018", "CHRONIC_KIDNEY_DISEASE_STAGE_3_4_OR_5")
    )
    Patient(
        getAgeInYearsAt($encounter.startDate) >= 18
    )
then
    insert(new EncounterDenominator($program, $encounter, 'Year2018.Mips122Rate1'));
end

rule 'Year2018.Mips122Rate1.Success.G8476'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2018.Mips122Rate1')
    (
        exists ClinicalActivity(
            startsDuring($encounter),
            withValueSet("MipsYear2018", "PERFORMANCE_MET_G8476")
        )
        or (
            exists ClinicalActivity(
                startsDuring($encounter),
                withValueSet("VsacYear2018", "SYSTOLIC_BLOOD_PRESSURE"),
                resultAsDecimal < 140
            )
            and exists ClinicalActivity(
                startsDuring($encounter),
                withValueSet("VsacYear2018", "DIASTOLIC_BLOOD_PRESSURE"),
                resultAsDecimal < 90
            )
        )
    )
then
    controlSet.addEncounterMeasureStatus($program, $encounter, 'Year2018.Mips122Rate1', MeasureStatusValue.SUCCESS, "G8476");
end

rule 'Year2018.Mips122Rate1.Gap.G8477'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2018.Mips122Rate1')
    (
        exists ClinicalActivity(
            startsDuring($encounter),
            withValueSet("MipsYear2018", "PERFORMANCE_NOT_MET_G8477")
        )
        or (
            exists ClinicalActivity(
                startsDuring($encounter),
                withValueSet("VsacYear2018", "SYSTOLIC_BLOOD_PRESSURE"),
                resultAsDecimal != null
            )
            and exists ClinicalActivity(
                startsDuring($encounter),
                withValueSet("VsacYear2018", "DIASTOLIC_BLOOD_PRESSURE"),
                resultAsDecimal != null
            )
        )
    )
then
    controlSet.addEncounterMeasureStatus($program, $encounter, 'Year2018.Mips122Rate1', MeasureStatusValue.GAP, "G8477");
end

rule 'Year2018.Mips122Rate1.Gaps.G8478'
when
    EncounterDenominator($program: program, $encounter: clinicalActivity, measure == 'Year2018.Mips122Rate1')
then
    controlSet.addEncounterMeasureStatus($program, $encounter, 'Year2018.Mips122Rate1', MeasureStatusValue.GAP, "G8478");
end
