package com.ablehealth;

import com.ablehealth.model.*
import com.ablehealth.payloads.*;
import com.ablehealth.results.*;

global PatientResults controlSet;

rule 'Year2019.Bcs1.Denominator'
when
	$program: Program(rules contains 'measure.Hedis.2019.Bcs1')
	$patient: Patient(
		getAgeInYearsAt($program.measurementPeriod.end) >= 52 &&
		getAgeInYearsAt($program.measurementPeriod.end) <= 74,
		isFemale()
	)
	and not ClinicalActivity(
		startsDuring($program.measurementPeriod),
		withValueSet("HedisYear2019", "HOSPICE")
	)
then
	insert(new PatientDenominator($program, $patient, 'Year2019.Bcs1'));
end

rule 'Year2019.Bcs1.Exclusion.exclusion_1'
when
	PatientDenominator($program: program, $patient: patient, measure == 'Year2019.Bcs1')
	(
		exists ClinicalActivity(
			startsOnOrBefore($program.measurementPeriod.end),
			withValueSet("HedisYear2019", "BILATERAL_MASTECTOMY")
		)
		or exists Diagnosis(
			startsOnOrBefore($program.measurementPeriod.end),
			withValueSet("HedisYear2019", "HISTORY_OF_BILATERAL_MASTECTOMY")
		)
		or exists ClinicalActivity(
			startsOnOrBefore($program.measurementPeriod.end),
			hasModifier("50") || hasModifier("09950"),
			withValueSet("HedisYear2019", "UNILATERAL_MASTECTOMY")
		)
		or (
			$clinical: ClinicalActivity(
				startsOnOrBefore($program.measurementPeriod.end),
				withValueSet("HedisYear2019", "UNILATERAL_MASTECTOMY")
			)
			and exists ClinicalActivity(
				startsOnOrBefore($program.measurementPeriod.end),
				startsOnOrAfter($clinical.startDate.plusDays(14)),
				withValueSet("HedisYear2019", "UNILATERAL_MASTECTOMY")
			)
		)
		or (
			(
				exists Diagnosis(
					startsOnOrBefore($program.measurementPeriod.end),
					withValueSet("HedisYear2019", "ABSENCE_OF_LEFT_BREAST")
				)
				or exists ClinicalActivity(
					startsOnOrBefore($program.measurementPeriod.end),
					withValueSet("HedisYear2019", "UNILATERAL_MASTECTOMY_LEFT")
				)
				or exists ClinicalActivity(
					startsOnOrBefore($program.measurementPeriod.end),
					hasModifier("LT"),
					withValueSet("HedisYear2019", "UNILATERAL_MASTECTOMY")
				)
			)
			and (
				exists Diagnosis(
					startsOnOrBefore($program.measurementPeriod.end),
					withValueSet("HedisYear2019", "ABSENCE_OF_RIGHT_BREAST")
				)
				or exists ClinicalActivity(
					startsOnOrBefore($program.measurementPeriod.end),
					withValueSet("HedisYear2019", "UNILATERAL_MASTECTOMY_RIGHT")
				)
				or exists ClinicalActivity(
					startsOnOrBefore($program.measurementPeriod.end),
					hasModifier("RT"),
					withValueSet("HedisYear2019", "UNILATERAL_MASTECTOMY")
				)
			)
		)
	)
then
	controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Bcs1', MeasureStatusValue.EXCLUSION, "exclusion_1");
end

rule 'Year2019.Bcs1.Exclusion.exclusion_2'
when
	PatientDenominator(
		$program: program,
		$patient: patient,
		measure == 'Year2019.Bcs1',
		$patient.getAgeInYearsAt($program.measurementPeriod.end) >= 66
	)
	(
		(
			(
				exists ClinicalActivity(
					startsDuring($program.measurementPeriod),
					withValueSet("HedisYear2019", "FRAILTY")
				)
				or exists Diagnosis(
					startsDuring($program.measurementPeriod),
					withValueSet("HedisYear2019", "FRAILTY")
				)
			)
			and (
				(
					$inpatientEncounter: ClinicalActivity(
						startsDuring($program.measurementPeriod),
						withValueSet("HedisYear2019", "ACUTE_INPATIENT")
					)
					and exists Diagnosis(
						startsDuring($inpatientEncounter),
						withValueSet("HedisYear2019", "ADVANCED_ILLNESS")
					)
				)
				or exists Medication(
					startsDuring($program.measurementPeriod),
					prescribed == true,
					withValueSet("NcqaYear2019", "DEMENTIA_MEDICATIONS")
				)
			)
		)
		or exists (
			$frailtyEncounter: ClinicalActivity(
				startsDuring($program.measurementPeriod),
				withValueSet("HedisYear2019", "FRAILTY")
			)
			and exists Diagnosis(
				startsDuring($frailtyEncounter),
				withValueSet("HedisYear2019", "FRAILTY")
			)
		)
		or accumulate (
			(
				$encounter: ClinicalActivity(
					startsDuring($program.measurementPeriod),
					withValueSet("HedisYear2019", "OUTPATIENT") ||
					withValueSet("HedisYear2019", "OBSERVATION") ||
					withValueSet("HedisYear2019", "ED") ||
					withValueSet("HedisYear2019", "NONACUTE_INPATIENT")
				)
				and exists Diagnosis(
					startsDuring($encounter),
					withValueSet("HedisYear2019", "ADVANCED_ILLNESS")
				)
			);
			$priorEncountersCount: count(1);
			$priorEncountersCount >= 2
		)
	)
then
	controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Bcs1', MeasureStatusValue.EXCLUSION, "exclusion_2");
end

rule 'Year2019.Bcs1.Success.performance_met'
when
	PatientDenominator($program: program, $patient: patient, measure == 'Year2019.Bcs1')
	exists ClinicalActivity(
		startsDuring(
			$program.measurementPeriod.end.plusDays(1).minusMonths(27),
			$program.measurementPeriod.end
		),
		withValueSet("HedisYear2019", "MAMMOGRAPHY") ||
		withValueSet("MipsYear2019", "PERFORMANCE_MET_G9899")
	)
then
	controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Bcs1', MeasureStatusValue.SUCCESS, "performance_met");
end

rule 'Year2019.Bcs1.Gap.performance_not_met'
when
	PatientDenominator($program: program, $patient: patient, measure == 'Year2019.Bcs1')
then
	controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Bcs1', MeasureStatusValue.GAP, "performance_not_met");
end
