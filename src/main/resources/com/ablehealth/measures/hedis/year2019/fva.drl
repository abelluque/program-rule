package com.ablehealth;

import com.ablehealth.model.*
import com.ablehealth.payloads.*;
import com.ablehealth.results.*;

global PatientResults controlSet;

rule 'Year2019.Hedis.Fva.Denominator'
when
    $program: Program(rules contains 'measure.Hedis.2019.Fva')
    $patient: Patient(
      getAgeInYearsAt($program.measurementPeriod.end) >= 18,
      getAgeInYearsAt($program.measurementPeriod.end) < 65
    )
then
    insert(new PatientDenominator($program, $patient, 'Year2019.Fva'));
end

rule 'Year2019.Hedis.Fva.SuccessReasons.PerformanceMet'
when
    PatientDenominator($program: program, $patient: patient, measure == 'Year2019.Fva')
    (
      exists ClinicalActivity(
          startsDuring($program.measurementPeriod.end.minusMonths(6).plusDays(1),$program.measurementPeriod.end),
          withValueSet("VsacYear2019", "INFLUENZA_VACCINATION") ||
          withValueSet("VsacYear2019", "PREVIOUS_RECEIPT_OF_INFLUENZA_VACCINE")
      )
      or exists Medication(
          startsDuring($program.measurementPeriod.end.minusMonths(6).plusDays(1),$program.measurementPeriod.end),
          withValueSet("VsacYear2019", "INFLUENZA_VACCINE")
      )
    )
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Fva', MeasureStatusValue.SUCCESS, 'performance_met');
end

rule 'Year2019.Hedis.Fva.GapReasons.PerformanceNotMet'
when
    PatientDenominator($program: program, $patient: patient, measure == 'Year2019.Fva')
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Fva', MeasureStatusValue.GAP, 'performance_not_met');
end
