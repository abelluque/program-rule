package com.ablehealth;

import com.ablehealth.model.*
import com.ablehealth.payloads.*;
import com.ablehealth.results.*;

global PatientResults controlSet;

rule 'Year2019.Aap1.Denominator'
when
    $program: Program(rules contains 'measure.Hedis.2019.Aap1')
    $patient: Patient(
      getAgeInYearsAt($program.measurementPeriod.end) >= 20
    )
then
    insert(new PatientDenominator($program, $patient, 'Year2019.Aap1'));
end

rule 'Year2019.Aap1.Success.performance_met'
when
    PatientDenominator($program: program, $patient: patient, measure == 'Year2019.Aap1')
    exists ClinicalActivity(
        startsDuring($program.measurementPeriod),
        withValueSet("HedisYear2019", "AMBULATORY_VISITS") ||
        withValueSet("HedisYear2019", "OTHER_AMBULATORY_VISITS") ||
        withValueSet("HedisYear2019", "TELEPHONE_VISITS") ||
        withValueSet("HedisYear2019", "ONLINE_ASSESSMENTS")
    )
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Aap1', MeasureStatusValue.SUCCESS, "performance_met");
end

rule 'Year2019.Aap1.Gap.performance_not_met'
when
    PatientDenominator($program: program, $patient: patient, measure == 'Year2019.Aap1')
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Aap1', MeasureStatusValue.GAP, "performance_not_met");
end
