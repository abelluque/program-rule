package com.ablehealth;

import com.ablehealth.model.*
import com.ablehealth.payloads.*;
import com.ablehealth.results.*;

import java.util.Collection;

global PatientResults controlSet;
global Collection<Provider> providers;

rule 'Year2019.Stage3pghd.Denominator'
when
    $program: Program(rules contains 'measure.Pi.2019.Stage3pghd')
    $encounter: Encounter(
        startsDuring($program.piMeasurementPeriod)
    )
then
    insert(new EncounterDenominator($program, $encounter, 'Year2019.Stage3pghd'));
end

rule 'Year2019.Stage3pghd.Success.performance_met.Org'
when
    EncounterDenominator($program: program, $encounter: encounter, measure == 'Year2019.Stage3pghd')
    $patient: Patient()
    exists SystemEvent(
        startsDuring($program.piMeasurementPeriod),
        eventType == "PGHD_INCORPORATION"
    )
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Stage3pghd', MeasureStatusValue.SUCCESS, "performance_met");
end

rule 'Year2019.Stage3pghd.Success.performance_met.Provider'
when
    EncounterDenominator($program: program, $encounter: encounter, measure == 'Year2019.Stage3pghd')
    $patient: Patient()
    exists SystemEvent(
        startsDuring($program.piMeasurementPeriod),
        eventType == "PGHD_INCORPORATION",
        providerExternalId == $encounter.providerExternalId
    )
then
    controlSet.addProviderPatientMeasureStatusesForMatchingProviders($program, $patient, $encounter.providerExternalId,'Year2019.Stage3pghd', MeasureStatusValue.SUCCESS, "performance_met", providers);
end

rule 'Year2019.Stage3pghd.Success.performance_met.Group'
when
    EncounterDenominator($program: program, $encounter: encounter, measure == 'Year2019.Stage3pghd')
    $patient: Patient()
    exists SystemEvent(
        startsDuring($program.piMeasurementPeriod),
        eventType == "PGHD_INCORPORATION",
        groupExternalId == $encounter.groupExternalId
    )
then
    controlSet.addGroupPatientMeasureStatus($program, $patient, $encounter.groupExternalId, 'Year2019.Stage3pghd', MeasureStatusValue.SUCCESS, "performance_met");
end

rule 'Year2019.Stage3pghd.Gap.performance_not_met'
when
    EncounterDenominator($program: program, $encounter: encounter, measure == 'Year2019.Stage3pghd')
    $patient: Patient()
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Stage3pghd', MeasureStatusValue.GAP, "performance_not_met");
    controlSet.addProviderPatientMeasureStatusesForMatchingProviders($program, $patient, $encounter.providerExternalId,'Year2019.Stage3pghd', MeasureStatusValue.GAP, "performance_not_met", providers);
    controlSet.addGroupPatientMeasureStatus($program, $patient, $encounter.groupExternalId, 'Year2019.Stage3pghd', MeasureStatusValue.GAP, "performance_not_met");
end
