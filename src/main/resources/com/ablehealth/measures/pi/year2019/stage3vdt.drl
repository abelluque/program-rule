package com.ablehealth;

import com.ablehealth.model.*
import com.ablehealth.payloads.*;
import com.ablehealth.results.*;

import java.util.Collection;

global PatientResults controlSet;
global Collection<Provider> providers;

rule 'Year2019.Stage3vdt.Denominator'
when
    $program: Program(rules contains 'measure.Pi.2019.Stage3vdt')
    $encounter: Encounter(
        startsDuring($program.piMeasurementPeriod)
    )
then
    insert(new EncounterDenominator($program, $encounter, 'Year2019.Stage3vdt'));
end

rule 'Year2019.Stage3vdt.Success.performance_met'
when
    EncounterDenominator($program: program, $encounter: encounter, measure == 'Year2019.Stage3vdt')
    $patient: Patient()
    exists SystemEvent(
        startsDuringYear($program.piMeasurementPeriod),
        eventType == "VDT_VIEW"
    )
    or exists SystemEvent(
        startsDuringYear($program.piMeasurementPeriod),
        eventType == "VDT_DOWNLOAD"
    )
    or exists SystemEvent(
        startsDuringYear($program.piMeasurementPeriod),
        eventType == "VDT_TRANSMISSION"
    )
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Stage3vdt', MeasureStatusValue.SUCCESS, "performance_met");
    controlSet.addProviderPatientMeasureStatusesForMatchingProviders($program, $patient, $encounter.providerExternalId,'Year2019.Stage3vdt', MeasureStatusValue.SUCCESS, "performance_met", providers);
    controlSet.addGroupPatientMeasureStatus($program, $patient, $encounter.groupExternalId, 'Year2019.Stage3vdt', MeasureStatusValue.SUCCESS, "performance_met");
end

rule 'Year2019.Stage3vdt.Gap.performance_not_met'
when
    EncounterDenominator($program: program, $encounter: encounter, measure == 'Year2019.Stage3vdt')
    $patient: Patient()
then
    controlSet.addPatientMeasureStatus($program, $patient, 'Year2019.Stage3vdt', MeasureStatusValue.GAP, "performance_not_met");
    controlSet.addProviderPatientMeasureStatusesForMatchingProviders($program, $patient, $encounter.providerExternalId,'Year2019.Stage3vdt', MeasureStatusValue.GAP, "performance_not_met", providers);
    controlSet.addGroupPatientMeasureStatus($program, $patient, $encounter.groupExternalId, 'Year2019.Stage3vdt', MeasureStatusValue.GAP, "performance_not_met");
end

